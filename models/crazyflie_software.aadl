package Crazyflie_Software
public
  with Crazyflie_Types;

  process nRF51822_Firmware
  features
    Syslink_Packet_TX: out data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_RX: in data port Crazyflie_Types::Syslink_Packet;
  flows
    f1 : flow source syslink_Packet_TX{ latency => 1 ms .. 2 ms; };
    f2 : flow sink syslink_Packet_RX{ latency => 1 ms .. 2 ms; };

  end nRF51822_Firmware;

  process STM32F405_Firmware
  features
    Syslink_Packet_Rx : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_Tx : out data port Crazyflie_Types::Syslink_Packet;
    DOFs           : in data port Crazyflie_Types::Six_Axis.impl;
    
    -- ADAM Q8 - Add the flowdeck input to the STM
    FLOWDECK       : in data port Crazyflie_Types::Flow_Deck.impl;

    Rate_1         : out data port Crazyflie_Types::PWM_Rate;
    Rate_2         : out data port Crazyflie_Types::PWM_Rate;
    Rate_3         : out data port Crazyflie_Types::PWM_Rate;
    Rate_4         : out data port Crazyflie_Types::PWM_Rate;

  	flows
    -- ADAM Q4 Add flows to rate control for other proppellers
    f1 : flow path Syslink_Packet_Rx -> Rate_1;
    f1b : flow path Syslink_Packet_Rx -> Rate_2;
    f1c : flow path Syslink_Packet_Rx -> Rate_3;
    f1d : flow path Syslink_Packet_Rx -> Rate_4;
    f2 : flow path DOFs              -> Rate_1;
    f2b : flow path DOFs              -> Rate_2;
    f2c : flow path DOFs              -> Rate_3;
    f2d : flow path DOFs              -> Rate_4;
    
    -- ADAM Q4: Add path for STM to send feedback to RF module
    f3 : flow path DOFs              -> Syslink_Packet_Tx;
    
    -- AAM Q8: Ad Flows for FLOWDECK
    f4  : flow path FLOWDECK          -> Rate_1;
    f4b  : flow path FLOWDECK          -> Rate_2;
    f4c : flow path FLOWDECK          -> Rate_3;
    f4d  : flow path FLOWDECK          -> Rate_4;
    
  annex EMV2 {**
    use types Crazyflie_Errors;
    use behavior Crazyflie_Errors::simple;

    error propagations
      Rate_1 : out propagation{ValueErroneous};
      DOFs: in propagation{ValueErroneous};
      Syslink_Packet_RX: in propagation{ValueErroneous};
      processor : in propagation{Lost, ValueErroneous};

   flows
     f1 : error path Syslink_Packet_RX -> Rate_1;
     f2 : error path DOFs{ValueErroneous} -> Rate_1{ValueErroneous};
     f3 : error path processor{lost, ValueErroneous} -> Rate_1{ValueErroneous};
    end propagations;

    component error behavior
     transitions
       t1 : Operational -[ DOFs{ValueErroneous} or
       	               --    processor{ValueErroneous} or
       	                   processor{Lost}
       ]-> Failed;
     propagations
       Failed -[]-> Rate_1{ValueErroneous}; -- XXX
     end component;

  **};
  end STM32F405_Firmware;

  process implementation STM32F405_Firmware.impl
  subcomponents
    CRTP_Tx_Task     : thread CRTP_Tx_Task;
    CRTP_Rx_Task     : thread CRTP_Rx_Task;
    Power_Management : thread Power_Management;
    Main_Loop        : thread Main_Loop;

  connections
    C1 : port CRTP_Tx_Task.Msg -> Syslink_Packet_Tx;
    C8 : port Main_Loop.Syslink_Packet_Tx -> CRTP_Tx_Task.SysLink_Packet;

    C2 : port Syslink_Packet_Rx -> CRTP_Rx_Task.Msg;
    C7 : port CRTP_Rx_Task.SysLink_Packet -> Main_Loop.Syslink_Packet_Rx;

    C3 : port Main_Loop.Rate_1 -> Rate_1;
    C4 : port Main_Loop.Rate_2 -> Rate_2;
    C5 : port Main_Loop.Rate_3 -> Rate_3;
    C6 : port Main_Loop.Rate_4 -> Rate_4;

    C9 : port DOFs -> Main_Loop.DOFs;

  	flows
  -- ADAM: Connect through the flows to the other proppellers 
    f1 : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7 -> Main_Loop.fs1 -> C3 -> Rate_1;
    f1b : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7 -> Main_Loop.fs1b -> C4 -> Rate_2;
    f1c : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7 -> Main_Loop.fs1c -> C5 -> Rate_3;
    f1d : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7 -> Main_Loop.fs1d -> C6 -> Rate_4;
    f2 : flow path DOFs -> C9 -> Main_Loop.fs2 -> C3 -> Rate_1;
    f2b : flow path DOFs -> C9 -> Main_Loop.fs2b -> C4 -> Rate_2;
    f2c : flow path DOFs -> C9 -> Main_Loop.fs2c -> C5 -> Rate_3;
    f2d : flow path DOFs -> C9 -> Main_Loop.fs2d -> C6 -> Rate_4;
    
  end STM32F405_Firmware.impl;  

  thread CRTP_Tx_Task
  features
    Msg : out data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet : in data port Crazyflie_Types::Syslink_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us;
  end CRTP_Tx_Task;

  thread CRTP_Rx_Task
  features
    Msg : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet : out data port Crazyflie_Types::Syslink_Packet;
  flows
    fs1 : flow path Msg -> Syslink_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us;
  end CRTP_Rx_Task;

  thread Power_Management
  properties
    Priority => 2;
    Dispatch_Protocol => Periodic;
    Period => 500 us;
    Compute_Execution_Time => 10 us .. 20 us;
  end Power_Management;

  thread Main_Loop
  features
    DOFs              : in data port Crazyflie_Types::Six_Axis.impl;
    
    -- Q8 Adam: Add flowdeck data to main loop so it can be used by the controller
    FLOWDECK              : in data port Crazyflie_Types::Flow_Deck.impl;

    Syslink_Packet_Rx : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_Tx : out data port Crazyflie_Types::Syslink_Packet;

    Rate_1            : out data port Crazyflie_Types::PWM_Rate;
    Rate_2            : out data port Crazyflie_Types::PWM_Rate;
    Rate_3            : out data port Crazyflie_Types::PWM_Rate;
    Rate_4            : out data port Crazyflie_Types::PWM_Rate;

  	flows
   -- ADAM: Connect the other rate controllers
    fs1 : flow path Syslink_Packet_Rx -> Rate_1;
    fs1b : flow path Syslink_Packet_Rx -> Rate_2;
    fs1c : flow path Syslink_Packet_Rx -> Rate_3;
    fs1d : flow path Syslink_Packet_Rx -> Rate_4;
    fs2 : flow path DOFs              -> Rate_1;
    fs2b : flow path DOFs              -> Rate_2;
    fs2c : flow path DOFs              -> Rate_3;
    fs2d : flow path DOFs              -> Rate_4;
    fs3 : flow path FLOWDECK           -> Rate_1;
    fs3b : flow path FLOWDECK           -> Rate_2;
    fs3c : flow path FLOWDECK           -> Rate_3;
    fs3d : flow path FLOWDECK           -> Rate_4;

  properties
    Priority => 3;
    Dispatch_Protocol => Periodic;
    Period => 2000 us;
    Compute_Execution_Time => 100 us .. 200 us;
  end Main_Loop;

  -- ADAM:
  -- Q8 START
  -- Extend the STM to include an input from the FlowDeck
  process implementation STM32F405_Firmware.impl_v2
    extends STM32F405_Firmware.impl
  connections
    C10 : 	port FLOWDECK -> Main_Loop.FLOWDECK;
  flows
    f4 : flow path FLOWDECK -> C10 -> Main_Loop.fs3 -> C3 -> Rate_1;
    f4b : flow path FLOWDECK -> C10 -> Main_Loop.fs3b -> C4 -> Rate_2;
    f4c : flow path FLOWDECK -> C10 -> Main_Loop.fs3c -> C5 -> Rate_3;
    f4d : flow path FLOWDECK -> C10 -> Main_Loop.fs3d -> C6 -> Rate_4;    
  end STM32F405_Firmware.impl_v2;
  -- ADAM:
  -- Q8 END

end Crazyflie_Software;